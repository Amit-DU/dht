dnl $Id: configure.in,v 1.45 2004/08/07 18:29:04 amdragon Exp $
dnl
dnl Process this file with autoconf to produce a configure script.
dnl

AC_INIT(acinclude.m4)
AM_INIT_AUTOMAKE(chord, 0.1)
AM_CONFIG_HEADER(config.h)

AC_CANONICAL_HOST

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_PATH_PROGS(M4, gm4 gnum4 m4, '$(top_srcdir)/missing')

dnl Check for GTK and allow conditional Makefile.am statements
dnl AM_PATH_GTK(1.2.0,,AC_MSG_WARN(gtk not available; vis will not be built))

PKG_CHECK_MODULES(GSTUFF, gtk+-2.0 >= 2.0, AC_MSG_WARN([Found GTK...]), AC_MSG_WARN([You don't have GTK. Vis won't be built]))
AM_CONDITIONAL(GTK_AVAILABLE, test "$GSTUFF_CFLAGS")
AC_SUBST(GSTUFF_CFLAGS)
AC_SUBST(GSTUFF_LIBS)

SFS_WFLAGS
AC_PROG_INSTALL
AC_PROG_LN_S
#AC_PROG_RANLIB

AM_DISABLE_SHARED
test -z "$target" && target=NONE
AM_PROG_LIBTOOL

SFS_SFS
SFS_CFLAGS
CHORD_BUILDALL

dnl setup db3
SFS_DB3
AC_SUBST(DBLIB)
AC_SUBST(DBDEPS)
DBLIB='$(top_builddir)/sfsrodb/libsfsrodb.a'
DBDEPS='$(top_builddir)/sfsrodb/libsfsrodb.a'
if test "${with_db3}" != no; then
	CPPFLAGS="$CPPFLAGS -DSLEEPYCAT"
	DBLIB="$DBLIB $DB3_LIB"
else
	DBLIB="$DBLIB"' $(top_builddir)/adb/libbtree.a'
	DBDEPS="$DBDEPS"' $(top_builddir)/adb/libbtree.a'
fi


dnl Make sure we have a mesg_buf capable libarpc
AC_MSG_CHECKING(for a good aclnt.h)
AC_EGREP_HEADER(rpccb_msgbuf,
		aclnt.h,
		AC_MSG_RESULT(found),
		AC_MSG_ERROR(aclnt.h that defines rpccb_msgbuf not found. Install the latest build of SFS.)
)

dnl Make sure we have sfscrypt.h
AC_MSG_CHECKING(for sfscrypt.h)
AC_EGREP_HEADER(sfspriv,
		sfscrypt.h,
		AC_MSG_RESULT(found),
		AC_MSG_ERROR(new style sfscrypt not found. Install the latest build of SFS.)
)

dnl let directories share header files
for lib in utils merkle chord dhc dhash sfsrodb svc lsd proxy; do
    CPPFLAGS="-I"'$(top_srcdir)'"/$lib $CPPFLAGS"
done
    CPPFLAGS="-I"'$(top_builddir)'"/svc $CPPFLAGS"

ETAGS_ARGS='-C /dev/null'
AC_SUBST(ETAGS_ARGS)

AC_OUTPUT(Makefile svc/Makefile utils/Makefile 
	sfsrodb/Makefile dhash/Makefile lsd/Makefile
	merkle/Makefile chord/Makefile chordcd/Makefile 
	proxy/Makefile devel/Makefile
	test/Makefile tst/Makefile
	melody/Makefile dhc/Makefile usenet/Makefile
	arpeggio/Makefile)
